CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

IF(USE_SYSTEM_GLM)
    FIND_PACKAGE(GLM REQUIRED)
ELSE(USE_SYSTEM_GLM)
    INCLUDE("${CMAKE_MODULE_PATH}/build_glm.cmake")
ENDIF(USE_SYSTEM_GLM)


IF(USE_SYSTEM_FREETYPE)
    FIND_PACKAGE(Freetype REQUIRED)
ELSE(USE_SYSTEM_FREETYPE)
    INCLUDE("${CMAKE_MODULE_PATH}/build_freetype.cmake")
ENDIF(USE_SYSTEM_FREETYPE)


FIND_PACKAGE(FontConfig REQUIRED)
FIND_PACKAGE(OpenGL REQUIRED)
FIND_PACKAGE(GLFW REQUIRED)
INCLUDE("${CMAKE_MODULE_PATH}/FindGLEWmx.cmake")


IF(FREETYPE_FOUND AND OPENGL_FOUND AND GLEWmx_FOUND AND GLFW_FOUND)
    ADD_DEFINITIONS(-DGLEW_MX)
ELSE(FREETYPE_FOUND AND OPENGL_FOUND AND GLEWmx_FOUND AND GLFW_FOUND)
    IF(NOT FREETYPE_FOUND)
        MESSAGE(FATAL_ERROR "FreeType Not Found")
    ELSEIF(NOT OPENGL_FOUND)
        MESSAGE(FATAL_ERROR "OpenGL Not Found")
    ELSEIF(NOT GLEWmx_FOUND)
        MESSAGE(FATAL_ERROR "GLEW-MX Not Found")
    ELSEIF(NOT GLFW_FOUND)
        MESSAGE(FATAL_ERROR "GLFW Not Found")
    ENDIF()
ENDIF(FREETYPE_FOUND AND OPENGL_FOUND AND GLEWmx_FOUND AND GLFW_FOUND)


IF(NOT UNIX)
    ADD_DEFINITIONS(-DFGDLL)
ENDIF(NOT UNIX)
# OS Definitions
IF(UNIX)
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
ELSE(UNIX) #Windows
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
ENDIF(UNIX)


INCLUDE_DIRECTORIES(
    ${CMAKE_INCLUDE_PATH}
    ${FREETYPE_INCLUDE_DIRS}
    ${GLM_INCLUDE_DIRS}
    ${OPENGL_INCLUDE_DIR}
    ${GLEW_INCLUDE_DIR}
    ${GLFW_INCLUDE_DIR}
    "${PROJECT_SOURCE_DIR}/include"
    "${PROJECT_SOURCE_DIR}/src"
    )

IF(UNIX)
    INCLUDE_DIRECTORIES("${FONTCONFIG_INCLUDE_DIR}")
ENDIF(UNIX)

IF(APPLE)
    FIND_PACKAGE(X11 REQUIRED)
    INCLUDE_DIRECTORIES(${X11_INCLUDE_DIR})
ENDIF()


FILE(GLOB api_headers
    "${PROJECT_SOURCE_DIR}/include/*.h"
    "${PROJECT_SOURCE_DIR}/include/fg/*.h"
    )

FILE(GLOB headers
    "*.hpp"
    "*.h"
    )

FILE(GLOB sources
    "*.c"
    "*.cpp"
    )

SOURCE_GROUP(API FILES ${api_headers})
SOURCE_GROUP(Headers FILES ${headers})
SOURCE_GROUP(Sources FILES ${sources})

ADD_LIBRARY(forge SHARED
    ${api_headers}
    ${headers}
    ${sources}
    )

TARGET_LINK_LIBRARIES(forge
    ${FONTCONFIG_LIBRARY}
    ${FREETYPE_LIBRARIES}
    ${OPENGL_gl_LIBRARY}
    ${GLFW_LIBRARY}
    ${GLEWmx_LIBRARY}
    )

INSTALL(TARGETS forge DESTINATION lib)
